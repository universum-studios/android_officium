// Global gradle configuration. This configuration will be used for all sub-projects.
apply from: './dependencies.gradle'

/**
 * Global build script configuration ===============================================================
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

/**
 * All projects global configuration ===============================================================
 */
allprojects {
    repositories {
        jcenter()
    }
}

/**
 * All projects global tasks =======================================================================
 */
/**
 * Task that cleans build directory of the root project.
 */
task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Task that cleans local artifacts directory.
 */
task cleanLocalArtifacts(type: Delete) {
    // delete 'artifacts/universum/studios/android'
}

/**
 * Task that updates local artifacts directory.
 */
task updateLocalArtifacts() {
    subprojects.findAll {
        if (it.name.startsWith("library")) {
            dependsOn ':' + it.name + ':updateLocalArtifacts'
        }
    }
}

/**
 * Task that deploys (uploads) all library modules into Maven local repository.
 */
task deployToMavenLocal() {
    subprojects.findAll {
        if (it.name.startsWith("library")) {
            dependsOn ':' + it.name + ':uploadArchives'
        }
    }
}

/**
 * Task that deploys (uploads) all library modules up to the Bintray repository.
 */
task deployToBintray() {
    subprojects.findAll {
        if (it.name.startsWith("library")) {
            dependsOn ':' + it.name + ':bintrayUpload'
        }
    }
}