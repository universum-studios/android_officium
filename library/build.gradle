/*
 * =================================================================================================
 *                             Copyright (C) 2016 Universum Studios
 * =================================================================================================
 *         Licensed under the Apache License, Version 2.0 or later (further "License" only).
 * -------------------------------------------------------------------------------------------------
 * You may use this file only in compliance with the License. More details and copy of this License
 * you may obtain at
 *
 * 		http://www.apache.org/licenses/LICENSE-2.0
 *
 * You can redistribute, modify or publish any part of the code written within this file but as it
 * is described in the License, the software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES or CONDITIONS OF ANY KIND.
 *
 * See the License for the specific language governing permissions and limitations under the License.
 * =================================================================================================
 */
apply plugin: 'com.android.library'

/**
 * Library options =================================================================================
 */
android {
    compileSdkVersion config.build.compileSdkVersion
    buildToolsVersion config.build.buildToolsVersion

    defaultConfig {
        minSdkVersion config.build.minSdkVersion
        targetSdkVersion config.build.targetSdkVersion
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner config.test.instrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    sourceSets {
        release.java.srcDirs = [
                "${rootProject.rootDir}/library-core/src/main/java",
                "${rootProject.rootDir}/library-account/src/main/java",
                "${rootProject.rootDir}/library-event-core/src/main/java",
                "${rootProject.rootDir}/library-event-common/src/main/java",
                "${rootProject.rootDir}/library-service/src/main/java",
                "${rootProject.rootDir}/library-sync/src/main/java"
        ]
        debug.java.srcDirs = release.java.srcDirs
        androidTest.java.srcDirs = [
                "${rootProject.rootDir}/library/src/androidTest/java",
                "${rootProject.rootDir}/library-core/src/androidTest/java",
                "${rootProject.rootDir}/library-account/src/androidTest/java",
                "${rootProject.rootDir}/library-event-core/src/androidTest/java",
                "${rootProject.rootDir}/library-event-common/src/androidTest/java",
                "${rootProject.rootDir}/library-service/src/androidTest/java",
                "${rootProject.rootDir}/library-sync/src/androidTest/java"
        ]
        androidTest.res.srcDirs = [
                "${rootProject.rootDir}/library-sync/src/androidTest/res"
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

/**
 * Library dependencies ============================================================================
 */
repositories {
    mavenLocal()
    maven { url repos.bintray.universumStudiosAndroid }
}

dependencies {
    /*
     * RELEASE DEPENDENCIES ------------------------------------------------------------------------
     */
    compile deps.androidSupport.annotations
    compile deps.universumStudios.logger
    compile deps.universumStudios.utils
    compile deps.universumStudios.crypto
    compile deps.other.squareupOtto
    compile deps.other.squareupOkio
    compile deps.other.squareupOkhttp
    compile (deps.other.squareupRetrofit) {
        exclude module: 'okhttp'
    }
    compile deps.other.googleGson

    /*
     * TEST DEPENDENCIES ---------------------------------------------------------------------------
     */
    androidTestCompile project(':test-core')
}

/**
 * Additional configuration ========================================================================
 */
apply from: 'analysis/analysis.gradle'
apply from: 'coverage/codacy.gradle'
apply from: "${rootProject.rootDir}/deploy/deploy.gradle"
apply from: 'github/github.gradle'