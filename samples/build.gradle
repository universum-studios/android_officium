apply plugin: 'com.android.application'

/**
 * Application options =============================================================================
 */
android {
    compileSdkVersion config.build.compileSdkVersion
    buildToolsVersion config.build.buildToolsVersion

    defaultConfig {
        applicationId project.property('samples.applicationId')
        versionName project.property('samples.versionName')
        versionCode Integer.valueOf((String) project.property('samples.versionCode'))
        minSdkVersion config.build.minSdkVersion
        targetSdkVersion config.build.targetSdkVersion
    }

    final String keystoreDirPath = "${System.getenv('ANDROID_UNIVERSUM_KEYSTORE')}/sample"
    final File keystoreFile = new File("${keystoreDirPath}/keystore.jks")
    final File keyPropertiesFile = new File("${keystoreDirPath}/key.properties")
    if (keystoreFile.exists() && keyPropertiesFile.exists()) {
        final Properties keyProperties = new Properties()
        keyProperties.load(new FileInputStream(keyPropertiesFile))
        //
        // The key.properties file should contain following data (each property at a single line):
        // <pre>
        // store.password=STORE_PASSWORD
        // key.alias=ALIAS_OF_THE_KEY
        // key.password=KEY_PASSWORD
        // </pre>
        //
        signingConfigs {
            production {
                storeFile keystoreFile
                storePassword keyProperties['store.password']
                keyAlias keyProperties['key.alias']
                keyPassword keyProperties['key.password']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.production
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

/**
 * Application dependencies ========================================================================
 */
repositories {
    mavenLocal()
    maven { url repos.bintray.universum.studios.android }
}

dependencies {
    implementation project(':library')
    implementation deps.android.support.design
    implementation deps.android.support.vectorDrawable
    implementation deps.android.support.recyclerView
    implementation deps.other.butterknife
    annotationProcessor deps.other.butterknifeProcessor
    implementation deps.universum.studios.support.samples
    implementation deps.universum.studios.logger
    implementation deps.universum.studios.utils
    implementation deps.universum.studios.crypto
    implementation deps.other.squareupOtto
    implementation deps.other.squareupOkio
    implementation deps.other.squareupOkhttp
    implementation(deps.other.squareupRetrofit) {
        exclude module: 'okhttp'
    }
    implementation deps.other.googleGson
}